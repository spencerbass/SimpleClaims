plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
    id 'com.gradleup.shadow' version '9.3.0'
}

version = project.version;

configurations {
    shadowBundle
    implementation.extendsFrom(shadowBundle)
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

repositories {
    mavenCentral()
}


def os = org.gradle.internal.os.OperatingSystem.current()
def userHome = System.getProperty("user.home")

def hytaleHome
def hytaleServerJar
if (os.isWindows()) {
    hytaleHome = "$userHome/AppData/Roaming/Hytale"
} else if (os.isMacOsX()) {
    hytaleHome = "$userHome/Library/Application Support/Hytale"
} else if (os.isLinux()) {
    hytaleHome = "$userHome/.local/share/Hytale"
} else {
    throw new GradleException("Unsupported OS for Hytale")
}

hytaleServerJar = "$hytaleHome/install/$patchline/package/game/$game_build/Server/HytaleServer.jar"



dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation files(hytaleServerJar)
    implementation(files(System.getProperty("user.home") + "/AppData/Roaming/Hytale/install/release/package/game/$game_build/Server/HytaleServer.jar"))
    //implementation 'org.xerial:sqlite-jdbc:3.45.1.0'
    shadowBundle 'org.slf4j:slf4j-simple:2.0.17'
    shadowBundle 'org.xerial:sqlite-jdbc:3.45.1.0'
}

test {
    useJUnitPlatform()
}

processResources {
    var expandProps = [
        'name'          : project.name,
    'version'       : project.version,
    'group'         : project.group,
    'description'   : mod_description,
    'website'       : website,
    'server_version': server_version,
    'entry_point'   : entry_point
    ]

    filesMatching(['manifest.json']) {
        expand expandProps
    }
    inputs.properties(expandProps)
}

def serverRunDir = file("$projectDir/run")

idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = "--allow-op --disable-sentry --assets=$hytaleHome/install/$patchline/package/game/latest/Assets.zip --mods=${file("src/main/").absolutePath}  --auth-mode=authenticated"
        workingDirectory = serverRunDir.absolutePath
    }
}

shadowJar {
    archiveClassifier.set('')
    mergeServiceFiles()

    configurations = [project.configurations.shadowBundle]

    manifest {
        attributes(
                'Main-Class': 'com.buuz135.simpleclaims.Main'
        )
    }
}

tasks.build {
    dependsOn("shadowJar")
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    if (providers.environmentVariable("HT_MAVEN_USERNAME").present) {
        repositories {
            maven() {
                url = "https://maven.hytale-modding.info/releases"
                credentials {
                    username = providers.environmentVariable("HT_MAVEN_USERNAME").get()
                    password = providers.environmentVariable("HT_MAVEN_PASSWORD").get()
                }
            }
        }
    }

}